{
  lib,
  stdenv,
  fetchurl,
  makeWrapper,
  autoPatchelfHook,
  gcc-unwrapped,
}:

let
  inherit (stdenv.hostPlatform) system;

  sources = {
    x86_64-linux = {
      url = "https://github.com/antinomyhq/forge/releases/download/v1.11.0/forge-x86_64-unknown-linux-gnu";
      hash = "sha256-b/XzJ2K4bClEQ1CL7iX3Mx/OSP6i3tDq1qD9kB/p018=";
    };
    aarch64-linux = {
      url = "https://github.com/antinomyhq/forge/releases/download/v1.11.0/forge-aarch64-unknown-linux-gnu";
      hash = "sha256-5N0PRFvZjycmeZfWMwZkRJur3NELDwA//Y6fFVZ3Cn4=";
    };
    x86_64-darwin = {
      url = "https://github.com/antinomyhq/forge/releases/download/v1.11.0/forge-x86_64-apple-darwin";
      hash = "sha256-AJq1bkTmgp/8xeFhBSA7D/w/4xqghN1uzj9PuDTal1M=";
    };
    aarch64-darwin = {
      url = "https://github.com/antinomyhq/forge/releases/download/v1.11.0/forge-aarch64-apple-darwin";
      hash = "sha256-AMNrDyfCfo2tTrmuGM2s6VylEg+4WBJDdRwBpL+kRyY=";
    };
  };
in
stdenv.mkDerivation rec {
  pname = "forge";
  version = "1.11.0";

  src = fetchurl sources.${system};

  nativeBuildInputs = [
    makeWrapper
  ]
  ++ lib.optionals stdenv.isLinux [
    autoPatchelfHook
  ];

  buildInputs = lib.optionals stdenv.isLinux [
    gcc-unwrapped.lib
  ];

  dontUnpack = true;

  installPhase = ''
    runHook preInstall

    install -Dm755 $src $out/bin/forge

    runHook postInstall
  '';

  meta = with lib; {
    description = "AI-Enhanced Terminal Development Environment - A comprehensive coding agent that integrates AI capabilities with your development environment";
    homepage = "https://github.com/antinomyhq/forge";
    changelog = "https://github.com/antinomyhq/forge/releases/tag/v${version}";
    license = licenses.mit;
    sourceProvenance = with sourceTypes; [ binaryNativeCode ];
    maintainers = with maintainers; [ ];
    mainProgram = "forge";
    platforms = [
      "x86_64-linux"
      "aarch64-linux"
      "x86_64-darwin"
      "aarch64-darwin"
    ];
  };
}
